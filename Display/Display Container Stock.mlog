Initialize:
	GetLinks:
		jump GetLinks lessThan @links 2
		set i 0
		LinkLoop:
			getlink link i
			sensor type link @type
			jump FoundDisplay equal type @logic-display
			sensor cap link @itemCapacity
			jump NextLink lessThan cap 10
			FoundContainer:
				set source link
				jump NextLink always
			FoundDisplay:
				set display link
				set displaySize 80
				set margin 0
			NextLink:
				op add i i 1
				jump LinkLoop lessThan i @links
			jump GetLinks equal display null
			jump GetLinks equal source null

	CountNonZeroItems:
		set i 0
		set rowCount 0
		CountLoop:
			lookup item material i
			sensor stock source material
			jump NextCount equal stock 0
			op add rowCount rowCount 1
			NextCount:
				op add i i 1
				jump CountLoop lessThan i 16

	Resize:
		set displayRows 4
		jump Font8Rows greaterThan rowCount 4
		Font4Rows:
			set charHeight 16
			set charWidth 10
			set stroke 2
			set charSpacing 2
			set rowSpacing 4
			set colSpacing 0
			set margin 2
			set columns 1
			set imageSize 16
			jump EndCalcFont always
		Font8Rows:
			set charHeight 12
			set charWidth 6
			set stroke 1
			set charSpacing 2
			set rowSpacing 4
			set colSpacing 0
			set margin 0
			set columns 2	
			set imageSize 10
		EndCalcFont:
	
	CalcBounds:
		op div halfColSp colSpacing 2
		op div halfRowSp rowSpacing 2
		op div halfWidth displaySize 2

		op add charOffset charWidth charSpacing
		op add rowOffset charHeight rowSpacing

		op mul allRowsH rowOffset 4
		op sub allRowsH allRowsH rowSpacing
		op sub vertMargin displaySize allRowsH
		op div vertMargin vertMargin 2
		op sub topBounds displaySize vertMargin
		set bottomBounds vertMargin

		op div bgWidth displaySize columns
		jump HalfWidth equal columns 2

		FullWidth:
			set leftBounds margin
			set bgLeft 0
			op sub rightBounds bgWidth margin
			jump CalcFont always
		
		HalfWidth:
			op sub rightBounds halfWidth halfColSp
			set bgLeft halfWidth
	
	CalcFont:
		op mul strokes stroke 3
		op sub freeSpace charHeight strokes
		op div halfFreeH freeSpace 2
		op floor midOffset halfFreeH 
		op add midOffset midOffset stroke
		op sub topOffset charHeight stroke
		op sub rightOffset charWidth stroke
		op sub halfRectH charHeight midOffset
		op sub halfVertH halfRectH stroke
		op div halfRowSp rowSpacing 2
		

	SetColors:
		set bgColor %000000
		set fontColor %ffff99
		set imgColor %ffffff

	CalcImageParams:
		op idiv imagePos imageSize 2
		op add imageX leftBounds imagePos

	Clear:
		draw clear 0 0 0
		draw col bgColor
		drawflush display
	
Start:
	jump GetLinks lessThan @links 2
	draw stroke stroke
	set startTime @time
	set i 0
	set row 0
	op sub curY topBounds charHeight

StartLine:
	set curX rightBounds
	op add midY curY midOffset
	op add topY curY topOffset
	ClearLine:
		op sub clearY curY halfRowSp
		draw col bgColor
		draw rect bgLeft clearY bgWidth rowOffset
		
	GetMaterial:
		lookup item material i
		sensor value source material
		jump NextItem equal value 0

		draw col imgColor
		draw image imageX midY material imageSize
		
	PrintDigits:
		op mod digit value 10
		op idiv value value 10
		
	DrawDigit:
		op sub curX curX charOffset
		draw col fontColor
		op add @counter @counter digit
		jump Draw0 equal digit 0
		jump Draw1 equal digit 1
		jump Draw2 equal digit 2
		jump Draw3 equal digit 3
		jump Draw4 equal digit 4
		jump Draw5 equal digit 5
		jump Draw6 equal digit 6
		jump Draw7 equal digit 7
		jump Draw8 equal digit 8
		jump Draw9 equal digit 9
	
	NextDigit:
		jump PrintDigits greaterThan value 0

	FinishLine:
		drawflush display
		op add row row 1
		jump NextLine equal columns 1
		jump NextLine notEqual row 4
		
	MoveToCol2:
		op add leftBounds halfWidth halfColSp
		op sub rightBounds displaySize margin
		op add imageX leftBounds imagePos
		set bgLeft 40
		op sub curY topBounds charHeight
		jump NextItem always

NextLine:
	op sub curY curY rowOffset
		
NextItem:
	op add i i 1
	jump Done greaterThanEq row 8
	jump StartLine lessThan i 16

Done:
	op sub cycleTime @time startTime
	set rowCount row

CheckResize:
	jump Need2Cols greaterThan rowCount 4
	Need1Col:
		jump Resize equal columns 2
		jump Start always
	Need2Cols: 
		jump Resize equal columns 1
		MoveToCol1:
			set leftBounds margin
			op sub rightBounds halfWidth halfColSp
			op add imageX leftBounds imagePos
			set bgLeft 0
			jump Start always

Draw0:
	draw lineRect curX curY charWidth charHeight
	jump NextDigit always

Draw1:
	op add rightX curX rightOffset
	draw lineRect rightX curY stroke charHeight
	jump NextDigit always

Draw2:
	op add rightX curX rightOffset
	draw lineRect curX midY charWidth stroke
	draw lineRect curX topY charWidth stroke
	draw lineRect curX curY charWidth stroke
	draw lineRect curX curY stroke halfVertH
	draw lineRect rightX midY stroke halfVertH
	jump NextDigit always
	
Draw3:
	op add rightX curX rightOffset
	draw lineRect curX midY charWidth stroke
	draw lineRect curX topY charWidth stroke
	draw lineRect curX curY charWidth stroke
	draw lineRect rightX curY stroke charHeight
	jump NextDigit always

Draw4:
	op add rightX curX rightOffset
	draw lineRect curX midY charWidth stroke
	draw lineRect curX midY stroke halfRectH
	draw lineRect rightX curY stroke charHeight
	jump NextDigit always

Draw5:
	op add rightX curX rightOffset
	draw lineRect curX midY charWidth stroke
	draw lineRect curX topY charWidth stroke
	draw lineRect curX curY charWidth stroke
	draw lineRect curX midY stroke halfVertH
	draw lineRect rightX curY stroke halfVertH
	jump NextDigit always
	
Draw6:
	draw lineRect curX curY charWidth halfRectH
	draw lineRect curX topY charWidth stroke
	draw lineRect curX midY stroke halfVertH
	jump NextDigit always

Draw7:
	op add rightX curX rightOffset
	draw lineRect curX topY charWidth stroke
	draw lineRect rightX curY stroke charHeight
	jump NextDigit always

Draw8:
	draw lineRect curX curY charWidth charHeight
	draw lineRect curX midY charWidth stroke
	jump NextDigit always

Draw9:
	op add rightX curX rightOffset
	draw lineRect curX midY charWidth halfRectH
	draw lineRect curX curY charWidth stroke
	draw lineRect rightX curY stroke halfVertH
	jump NextDigit always