Initialize:
	set unitType @zenith
	set maxUnits 4
	
	GetLinks:
		set switch switch1
		jump GetLinks equal switch null

		# if flagging without a proc, set it to @this
		set targetProc processor1
		jump GetLinks equal targetProc null

GetId:
	sensor procX targetProc @x
	sensor procY targetProc @y
	op shl procId procX 10 # first 10 bits for x
	op add procId procId procY # second 10 bits for y

	# statuses
	set NONE 0
	set COUNTING 1
	set FLAGGING 2
	set RELEASING 3
	
Start:
CheckActive:
	sensor active switch @enabled
	jump Reset equal active false
	
SelectAction:
	jump Count equal countDone false
	jump Flag lessThan totalFlagged maxUnits
	jump Unflag greaterThan totalFlagged maxUnits
	jump FinishRun always
	
Count:
	set status COUNTING
	set i 0
	set flaggedCount 0
	set freeCount 0
	set firstUnit null
	CountLoop:
		ubind unitType
		jump Start equal @unit null
		sensor isDead @unit @dead
		jump Start equal isDead true
	
		jump FinishCount equal firstUnit @unit
		jump CountProcess notEqual firstUnit null
		set firstUnit @unit
	
	CountProcess:
		sensor flag @unit @flag
		jump CountFlagged equal flag procId
		jump CountFree equal flag 0
		jump NextCount always
		
	CountFlagged:
		op add flaggedCount flaggedCount 1
		jump NextCount always

	CountFree:
		op add freeCount freeCount 1
	
	NextCount:
		op add i i 1
		jump CountLoop always

	FinishCount:
		set totalFlagged flaggedCount
		set totalFree freeCount
		set totalUnits i
		set countDone true
		jump Start always

Flag:
	op sub neededFlags maxUnits totalFlagged
	op min neededFlags neededFlags totalFree
	jump FinishRun lessThanEq neededFlags 0
	
	set usedFlag procId
	set grabbedFlag 0
	jump RunFlagLoop always

Unflag:
	op sub neededFlags totalFlagged maxUnits
	jump FinishRun lessThanEq neededFlags 0
	set usedFlag 0
	set grabbedFlag procId
	jump RunFlagLoop always

UnflagAll:
	op sub neededFlags totalFlagged maxUnits
	jump FinishRun lessThanEq neededFlags 0
	set usedFlag 0
	set grabbedFlag procId
	jump RunFlagLoop always

RunFlagLoop:
	set status FLAGGING
	set flagged 0
	set firstUnit null
	FlagLoop:
		ubind unitType
		jump Start equal @unit null
		sensor isDead @unit @dead
		jump FlagLoop equal isDead true
	
		jump FinishRun equal firstUnit @unit
		jump FinishRun greaterThanEq flagged neededFlags
		jump FlagProcess notEqual firstUnit null
		set firstUnit @unit
	
	FlagProcess:
		sensor flag @unit @flag
		jump FlagLoop equal flag usedFlag
		jump FlagLoop notEqual flag grabbedFlag

		ucontrol flag usedFlag
		op add flagged flagged 1
		jump FlagLoop always
	
FinishRun:
	set countDone false
	jump Start always
	
Reset:
	jump Start equal status NONE
	set totalFlagged 0
	set totalFree 0
	set totalUnits 0

ReleaseAll:
	# wait a little because the other processor
	# might be doing its own releasing on the flagged units
	wait 6
	set status RELEASING
	set neededUnflags maxUnits
	set firstUnit null

	ReleaseLoop:
		ubind unitType
		jump FinishRelease equal @unit null
		jump FinishRelease equal firstUnit @unit
		jump ReleaseUnit notEqual firstUnit null
		set firstUnit @unit
	
		ReleaseUnit:
			sensor flag @unit @flag
			jump ReleaseLoop notEqual flag procId
			ucontrol flag 0
			jump ReleaseLoop always

	FinishRelease:
		set countDone false
		set status NONE
		jump Start always